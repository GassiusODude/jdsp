plugins {
    id "java"
    id "maven-publish"
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

// tell the exec, compile and test to allow incubator
tasks.withType(JavaCompile).configureEach {
    options.compilerArgs += [
        "--add-modules", "jdk.incubator.vector"
    ]
}
// tasks.withType(Javadoc).configureEach {
//     options.addStringOption('module-path', classpath.asPath)
//     options.addStringOption('add-modules', 'jdk.incubator.vector')
// }
tasks.withType(Javadoc).configureEach {
    options.addBooleanOption('html5', true)

    // Pass JVM arguments to the Javadoc tool
    options.addStringOption('-add-modules', 'jdk.incubator.vector')
}

tasks.withType(JavaExec).configureEach {
    jvmArgs += [
        "--add-modules", "jdk.incubator.vector"
    ]
}
tasks.withType(Test).configureEach {
    jvmArgs += ["--add-modules", "jdk.incubator.vector"]
}

tasks.register("runJar", JavaExec) {
    mainClass.set("net.kcundercover.jdsp.example.JdspExamples")
    classpath = sourceSets.main.runtimeClasspath
    jvmArgs = ["--add-modules", "jdk.incubator.vector"]
}


version = "1.1" // keep as a semantic version

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'junit:junit:4.13.2'
    implementation("org.ejml:ejml-all:0.44.0")
}

jar {
    manifest {
        attributes(
            "Application-Name": "JDSP Library",
            "Main-Class": "net.kcundercover.jdsp.example.JdspExamples",
            "Built-By": "Keith Chow",
            'Build-Timestamp': new java.text.SimpleDateFormat("yyyy-MM-dd\'T\'HH:mm:ss.SSSZ").format(new Date()),
            'Created-By': "Gradle ${gradle.gradleVersion}",
            'Build-Jdk': "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
            'Build-OS': "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}",
            'Codebase': "kcundercover.net",
            'Trusted-Library': true
        )
    }
}

// ----------------- Publish to Maven Local -----------------
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            groupId = "net.kcundercover"
            artifactId = "jdsp"
            version = project.version
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/GassiusODude/jdsp")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.token") ?: System.getenv("TOKEN")
            }
        }
    }
}
